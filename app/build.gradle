import javax.annotation.Nullable

apply plugin: 'com.android.application'

def getProperties(String fileName) {
    final Properties properties = new Properties()
    def file = file(fileName)
    if (file.exists()) {
        file.withInputStream { stream -> properties.load(stream) }
    }
    return properties
}

static def getProperty(@Nullable Properties properties, String name) {
    return properties.getProperty(name) ?: "$name missing"
}

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30

        renderscriptTargetApi 30
        vectorDrawables.useSupportLibrary = true

        applicationId 'com.kabouzeid.gramophone'
        versionCode 179
        versionName '1.3.5' + getGitHeadRefsSuffix()

        android.applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "Phonograph.apk"
            }
        }
    }
    signingConfigs {
        release {
            Properties properties = getProperties('/Users/karim/Documents/Phonograph/privatekeys/keystore.properties')
            storeFile file(getProperty(properties, 'storeFile'))
            keyAlias getProperty(properties, 'keyAlias')
            storePassword getProperty(properties, 'storePassword')
            keyPassword getProperty(properties, 'keyPassword')
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            ext.enableCrashlytics = false
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE', 'META-INF/NOTICE']
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    lint {
        abortOnError false
        disable 'MissingTranslation', 'InvalidPackage'
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.3.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.core:core:1.7.0'
    implementation 'androidx.fragment:fragment:1.4.1'
    implementation 'androidx.media:media:1.6.0'
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'androidx.percentlayout:percentlayout:1.0.0'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.google.android.material:material:1.6.0'

    implementation 'com.github.kabouzeid:app-theme-helper:1.3.10'
    implementation 'com.github.kabouzeid:RecyclerView-FastScroll:1.0.16-kmod'
    implementation 'com.github.kabouzeid:SeekArc:1.2-kmod'
    implementation 'com.github.kabouzeid:AndroidSlidingUpPanel:6'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.afollestad.material-dialogs:commons:0.9.6.0'
    implementation 'com.afollestad:material-cab:0.1.12'
    implementation 'com.github.ksoichiro:android-observablescrollview:1.6.0'
    implementation 'com.heinrichreimersoftware:material-intro:2.0.0'
    implementation 'me.zhanghai.android.materialprogressbar:library:1.6.1'
    implementation('com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:1.0.0@aar') {
        transitive = true
    }

    implementation 'com.jakewharton:butterknife:10.2.3'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.3'

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.google.code.gson:gson:2.9.0'

    implementation 'de.psdev.licensesdialog:licensesdialog:2.2.0'

    implementation 'com.github.bumptech.glide:glide:3.8.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:1.5.0'
    implementation 'org.eclipse.mylyn.github:org.eclipse.egit.github.core:2.1.5'

    implementation 'com.github.AdrienPoupa:jaudiotagger:2.2.3'
}
